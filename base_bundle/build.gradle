apply from: 'config/buildsystem/ci.gradle'
apply from: 'config/buildsystem/dependencies.gradle'
apply from: 'config/quality/generateReports.gradle'
apply plugin: 'dan-plugin'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/racobos/maven"
            //url  "../repo"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.racobos:danPlugin:1.0.1'
    }
}

allprojects {
    ext {
        androidApplicationId = packageSyntax
        androidVersionCode = 1
        androidVersionName = "1.0"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = packageSyntax + '.test'
    }
    repositories {
        maven { url "https://clojars.org/repo/" }
        maven { url 'http://dev.open-rnd.net:30844/content/groups/public/' }
    }

    task wrapper(type: Wrapper) {
        description 'Creates the gradle wrapper.'
        gradleVersion '2.8'
    }


    task runDomainUnitTests(dependsOn: [':domain:test']) {
        description 'Run unit tests for the domain layer.'
    }

    task runDataUnitTests(dependsOn: [':data:cleanTestDebugUnitTest', ':data:testDebugUnitTest']) {
        description 'Run unit tests for the data layer.'
    }

    task runPresentationUnitTests(
            dependsOn: [':presentation:cleanTestDebugUnitTest', ':presentation:testDebugUnitTest']) {
        description 'Run unit tests for the presentation layer.'
    }
    task runUnitTests(dependsOn: ['runPresentationUnitTests', 'runDomainUnitTests', 'runDataUnitTests']) {
        description 'Run unit tests for presentation, domain and data layers.'
    }

    task runAcceptanceTests(dependsOn: [':presentation:connectedAndroidTest']) {
        description 'Run application acceptance tests.'
    }

    task runTests(dependsOn: ['runUnitTests', ':presentation:connectedAndroidTest']) {
        description 'Run all tests.'
    }
}