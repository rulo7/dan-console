apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.racobos'
version = '0.0.3'

repositories {
    mavenCentral()
}
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        java {
            srcDir 'src/main'
            exclude '**/resources/base_bundle/**'
        }
    }
}

dependencies {
    compile gradleApi()
    compile 'net.lingala.zip4j:zip4j:1.3.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'maven'
        name = 'danPlugin'
        licenses = ['MIT']
        publish = true
        vcsUrl = 'https://github.com/rulo7/dan-console.git'
        publications = ['Publication']
        version {
            name = '0.0.3-Experimental'
        }
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            groupId 'com.racobos'
            artifactId 'danPlugin'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../../repo'))
        }
    }
}

task zipBundle(type: Zip) {
    from ('../base_bundle/')
    archiveName 'base_bundle.zip'
    destinationDir file('src/main/resources/') // directory that you want your archive to be placed in
}

task localUpload(dependsOn:['zipBundle','uploadArchives'])
task remoteUpload(dependsOn:['zipBundle','publishToMavenLocal','install','bintrayUpload'])

uploadArchives.mustRunAfter zipBundle
publishToMavenLocal.mustRunAfter zipBundle
install.mustRunAfter publishToMavenLocal
bintrayUpload.mustRunAfter install